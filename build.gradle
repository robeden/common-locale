apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'


group = 'com.logicartisan'
archivesBaseName = 'common-locale'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1';

    testCompile group: 'junit', name: 'junit', version: '4.12'
}





task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}


signing {
    sign configurations.archives
}


javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}



uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'LogicArtisan Common Locale'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Localization framework for Java with compile-time verification.'
                url 'https://bitbucket.org/logicartisan/common-locale'

                scm {
                    connection 'scm:git:https://bitbucket.org/logicartisan/common-locale.git'
                    developerConnection 'scm:git:https://bitbucket.org/logicartisan/common-locale.git'
                    url 'https://bitbucket.org/logicartisan/common-locale/src'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'robeden'
                        name 'Rob Eden'
                        email 'robeden1@gmail.com'
                    }
                }
            }
        }
    }
}
uploadArchives.dependsOn sourcesJar
uploadArchives.dependsOn javadocJar